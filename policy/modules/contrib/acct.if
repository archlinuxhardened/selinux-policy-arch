## <summary>Berkeley process accounting.</summary>

########################################
## <summary>
##	Transition to the accounting
##	management domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`acct_domtrans',`
	gen_require(`
		type acct_t, acct_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, acct_exec_t, acct_t)
')

########################################
## <summary>
##	Execute accounting management tools
##	in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`acct_exec',`
	gen_require(`
		type acct_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, acct_exec_t)
')

########################################
## <summary>
##	Execute accounting management data
##	in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`acct_exec_data',`
	gen_require(`
		type acct_data_t;
	')

	files_search_var($1)
	can_exec($1, acct_data_t)
')

########################################
## <summary>
##	Create, read, write, and delete
##	process accounting data.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`acct_manage_data',`
	gen_require(`
		type acct_data_t;
	')

	files_search_var($1)
	manage_files_pattern($1, acct_data_t, acct_data_t)
	manage_lnk_files_pattern($1, acct_data_t, acct_data_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an acct environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`acct_admin',`
	gen_require(`
		type acct_t, acct_initrc_exec_t, acct_data_t;
	')

	allow $1 acct_t:process { ptrace signal_perms };
	ps_process_pattern($1, acct_t)

	init_startstop_service($1, $2, acct_t, acct_initrc_exec_t)

	logging_search_logs($1)
	admin_pattern($1, acct_data_t)
')
