## <summary>E-mail security and anti-spam package for e-mail gateway systems.</summary>

########################################
## <summary>
##	Create, read, write, and delete
##	mscan spool content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mscan_manage_spool_content',`
	gen_require(`
		type mscan_spool_t;
	')

	files_search_spool($1)
	manage_dirs_pattern($1, mscan_spool_t, mscan_spool_t)
	manage_files_pattern($1, mscan_spool_t, mscan_spool_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an mscan environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`mscan_admin',`
	gen_require(`
		type mscan_t, mscan_etc_t, mscan_initrc_exec_t;
		type mscan_var_run_t, mscan_spool_t;
	')

	allow $1 mscan_t:process { ptrace signal_perms };
	ps_process_pattern($1, mscan_t)

	init_startstop_service($1, $2, mscan_t, mscan_initrc_exec_t)

	files_search_etc($1)
	admin_pattern($1, mscan_etc_t)

	files_search_pids($1)
	admin_pattern($1, mscan_var_run_t)

	files_search_spool($1)
	admin_pattern($1, mscan_spool_t)
')
