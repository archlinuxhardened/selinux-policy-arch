## <summary>Standards Based Linux Instrumentation for Manageability.</summary>

########################################
## <summary>
##	Execute gatherd in the gatherd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`sblim_domtrans_gatherd',`
	gen_require(`
		type sblim_gatherd_t, sblim_gatherd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, sblim_gatherd_exec_t, sblim_gatherd_t)
')

########################################
## <summary>
##	Read gatherd pid files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sblim_read_pid_files',`
	gen_require(`
		type sblim_var_run_t;
	')

	files_search_pids($1)
	allow $1 sblim_var_run_t:file read_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an sblim environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`sblim_admin',`
	gen_require(`
		attribute sblim_domain;
		type sblim_initrc_exec_t, sblim_var_run_t;
	')

	allow $1 sblim_domain:process { ptrace signal_perms };
	ps_process_pattern($1, sblim_domain)

	init_startstop_service($1, $2, sblim_domain, sblim_initrc_exec_t)

	files_search_pids($1)
	admin_pattern($1, sblim_var_run_t)
')
